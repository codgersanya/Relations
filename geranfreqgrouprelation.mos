#This script adds Lrat.GeranFreqGroupFreqRelations -based on existing GeranFreqGroups.
#GeranFreqGroups must be defined before running this script!
#The recent parameter master doesn't include any UtranFreqRelation-relevant settings -except qRxLevMin (-105dBm).
#Hence rest of the priority-settings will be set to the last known-settings:
#GeranFreqGroupRelation.altCsfbTargetPrio 7
#GeranFreqGroupRelation.altCsfbTargetPrioEc 7
#GeranFreqGroupRelation.cellReselectionPriority 1
#GeranFreqGroupRelation.connectedModeMobilityPrio -1 
#GeranFreqGroupRelation.csFallbackPrio 5
#GeranFreqGroupRelation.csFallbackPrioEC 5
#GeranFreqGroupRelation.voicePrio -1

lt all
unset all
$return = 0
!rm -r $tempdir/*.*

!echo '*****************************************************' >> $tempdir/temp_error1.txt
!echo '** ERROR: There are no EUtranCellFDDs!***************' >> $tempdir/temp_error1.txt
!echo '** Please define them before running this script! ***' >> $tempdir/temp_error1.txt
!echo '** Script execution is stopped **********************' >> $tempdir/temp_error1.txt
!echo '*****************************************************' >> $tempdir/temp_error1.txt
 
!echo '*********************************************************' >> $tempdir/temp_error2.txt
!echo '** ERROR: There are no GeranFreqGroups! *****************' >> $tempdir/temp_error2.txt
!echo '** Please define all GeranFreqGroup(s)  first! **********' >> $tempdir/temp_error2.txt
!echo '*********************************************************' >> $tempdir/temp_error2.txt

!echo '**************************************************************************' >> $tempdir/temp_success.txt
!echo '** SUCCESS: The following GeranFreqGroupFreqRelations have been added: ***' >> $tempdir/temp_success.txt
!echo '**************************************************************************' >> $tempdir/temp_success.txt

!echo '*************************************************************************' >> $tempdir/temp_nonew.txt
!echo '** NO CHANGE: All GeranFreqGroupFreqRelations are added already. ********' >> $tempdir/temp_nonew.txt
!echo '** The script has finished with the check! ******************************' >> $tempdir/temp_nonew.txt
!echo '*************************************************************************' >> $tempdir/temp_nonew.txt

mr enbf
ma enbf ^enodebfunction=
for $mo in enbf
	$enbfldn = ldn($mo)
done

pr ^EUtranCellFDD=
if $nr_of_mos = 0
	!cat  $tempdir/temp_error1.txt
	l+mmo $tempdir/dummy
	$return = 1
	return
fi

mr cellgroup
ma cellgroup ^EUtranCellFDD=
	for $mo in cellgroup
		$i = $i + 1
		$ldnEUtranCellFDD[$i] = ldn($mo)
		$moidEUtranCellFDD[$i] = $ldnEUtranCellFDD[$i] -s .*=
	done

pr ^GeranFreqGroup=
if $nr_of_mos = 0
	!cat  $tempdir/temp_error2.txt
	return
fi

mr GeranFreqGroup
ma GeranFreqGroup ^GeranFreqGroup=
	for $mo in GeranFreqGroup
		$j = $j + 1
		$ldnGeranFreqGroup[$j] = ldn($mo)
		$moidGeranFreqGroup[$j] = $ldnGeranFreqGroup[$j] -s .*=
		$geranFreqGroupRef[$j] = $ldnGeranFreqGroup[$j] -s $enbfldn,
done

func result
	pr $ldnEUtranCellFDD[$m],GeranFreqGroupRelation=$moidGeranFreqGroup[$k]
	if nr_of_mos != 0
	!echo $ldnEUtranCellFDD[$m],GeranFreqGroupRelation=$moidGeranFreqGroup[$k] >> $tempdir/result.txt
	fi
endfunc

func add_GeranFreqGroup_to_cell
	for $k = 1 to $j
		get $ldnEUtranCellFDD[$m],GeranFreqGroupRelation= geranFreqGroupRef $geranFreqGroupRef[$k]
		if $nr_of_mos = 0
			crn $ldnEUtranCellFDD[$m],GeranFreqGroupRelation=$moidGeranFreqGroup[$k]
			geranFreqGroupRef $geranFreqGroupRef[$k]
			end
			result
		fi
	done
endfunc

func nonew
	$linecontent = testfile($tempdir/result.txt)
	if $nr_of_lines  = 0
		!cat $tempdir/temp_nonew.txt
	else
		!cat $tempdir/temp_success.txt
		!cat $tempdir/result.txt
	fi
endfunc

for $m = 1 to $i
	add_GeranFreqGroup_to_cell
done

#specific settings starts here
set ^GeranFreqGroupRelation= qRxLevMin -105
set ^GeranFreqGroupRelation= altCsfbTargetPrio 7
set ^GeranFreqGroupRelation= altCsfbTargetPrioEc 7
set ^GeranFreqGroupRelation= cellReselectionPriority 1
set ^GeranFreqGroupRelation= connectedModeMobilityPrio -1
set ^GeranFreqGroupRelation= csFallbackPrio 5
set ^GeranFreqGroupRelation= csFallbackPrioEC 5
set ^GeranFreqGroupRelation= voicePrio -1
#specific settings ends here

nonew
